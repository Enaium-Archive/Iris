buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.enaium.cn' }
    }

    dependencies {
        classpath 'cn.enaium:ml4g:1.0.1'
    }
}

apply plugin: "java"
apply plugin: 'ml4g'

group 'cn.enaium'
version '1.0'
def id = "iris"
def cf4mVersion = "1.5.2"
def mcVersion = "21w14a"

minecraft {
    gameVersion = mcVersion
    mainClass = "net.minecraft.launchwrapper.Launch"
    tweakClass = "cn.enaium.iris.launch.ClientTweaker"
    mixinRefMap = "mixins.${id}.refmap.json"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("cn.enaium.cf4m:cf4m:${cf4mVersion}")
    implementation('org.spongepowered:mixin:0.8.2')
    implementation('com.google.guava:guava:30.1-jre')
    implementation('com.google.code.gson:gson:2.8.6')
    implementation('org.ow2.asm:asm-tree:9.1')
    implementation('org.ow2.asm:asm-commons:9.1')
    implementation('org.ow2.asm:asm-util:9.1')
    implementation('net.minecraft:launchwrapper:1.12') { transitive(false) }
}
processResources {
    filesMatching('profile.json') {
        expand([
                'version'      : project.version,
                'name'         : rootProject.name,
                'group'        : project.group,
                'id'           : "${id}",
                "mcVersion"    : "${mcVersion}",
                'cf4mVersion'  : "${cf4mVersion}",
                'formattedTime': new Date().format("yyyy-MM-dd'T'HH:mm:ss'-08:00'")
        ])
    }
}

jar {
    manifest {
        attributes([
                "Main-Class"              : "${project.group}.${id}.launch.Main",
                "Specification-Title"     : "${rootProject.name}",
                "Specification-Vendor"    : "Enaium",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : "${rootProject.name}",
                "Implementation-Version"  : "${project.version}",
                "Implementation-Vendor"   : "${project.group}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

artifacts {
    archives jar
}